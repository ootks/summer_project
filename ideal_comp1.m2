loadPackage("SymbolicPowers")

R = QQ[x,y,z,w]

-- First compute the ideal defining the union of 4 skew lines and 4 points in P^3
ideals = {
ideal(z,w), -- line1
ideal(x,y), --line2
ideal(x-z,w-y), --line3
ideal(y-z,x,w), --point1
ideal(x-w,y,z), --point2 
ideal(x,y-w,z-w), --point3
ideal(x-w,y-2*w,z-3*w) --point4
}
inter = intersect(ideals)
interGens = first entries mingens inter
-- Assert that inter is generated by cubics
for i to (length interGens)-1 do assert((first degree(interGens_i)) == 3)
f = sum(for i to (length interGens)-1 list (interGens_i)^2)
print(first degree(f))

-- Ideal generated by degree 6 polynomials
sextics = (ideal(x,y,z,w))^6
-- Remove all polynomials of degree >= 7 from R
septics = (ideal(x,y,z,w))^7
phi = map(R/septics, R)

-- normal square
square = trim(intersect(inter * inter, sextics))
-- squareDim is the dimension of the vector space of sextics in square
squareDim = length (first entries mingens phi(square))
-- Remove all polynomials from the square
psi = map(R/square, R)

-- symbolic square
symbSquare = trim(intersect(symbolicPower(inter, 2), sextics))
-- symbDim is the dimension of the vector space of sextics in symbSquare
symbDim = length (first entries mingens phi(symbSquare))
-- There should be exactly one extra dimension of sextic in symbSquare
assert(symbDim == squareDim + 1)

-- Generator for symbSquare / square
x = first first entries mingens psi(symbSquare)
g = lift(x, R)

T = R[e]
-- Our purported nonnegative polynomial that is not SOS.
h = f - e*g
-- Write h to file.
"nonnegativemaybe" << mathML h << endl << close
